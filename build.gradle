plugins {
    id 'java'
}

final buildFilename = "artifact.jar"
final minAPI = 26
group 'sonnicon'

sourceSets.main {
    java.srcDir("src/")
    resources.srcDir("assets/")
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {
    implementation 'com.github.Anuken.Mindustry:core:v121.4'
}

jar {
    archiveName buildFilename
}

task dexify(type: Jar) {
    archiveName "dexed-$buildFilename"

    final File jarArtifact = new File(tasks.jar.archiveFile.get().asFile.parent, buildFilename),
               dexedArtifact = new File(tasks.dexify.getTemporaryDir(), "dexed.jar")
    doFirst {
        exec {
            workingDir dexedArtifact.parent
            def command = ["d8", "--min-api", minAPI, "--output", dexedArtifact, jarArtifact]
            if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows'))
                commandLine("cmd", "/c", *command)
            else
                commandLine(*command)
        }
    }

    from( zipTree(jarArtifact), zipTree(dexedArtifact))
}

task buildDex dependsOn "build", "dexify"
